buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'com.spaceshift'
version = '1.9.5'

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

ext.jmeVersion = "3.2_branch-SNAPSHOT"
ext.junitPlatformVersion = "1.0.0"
ext.junitJupiterVersion = "5.0.0"
ext.log4jVersion = '2.6.2'
ext.lwjglVersion = "3.1.0"

junitPlatform {
    filters {
        engines {
        }
        tags {
        }
    }
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile 'com.github.JavaSaBr:SimFX:1.0.5'
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-core:$jmeVersion"
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-effects:$jmeVersion"
    compile "com.github.JavaSaBr.jmonkeyengine:jme3-bullet:$jmeVersion"

    testCompile 'com.github.JavaSaBr:Rlib:6.4.1'
    testCompile "org.junit.platform:junit-platform-commons:$junitPlatformVersion"
    testRuntime "org.junit.platform:junit-platform-engine:$junitPlatformVersion"

    testRuntime "com.github.JavaSaBr.jmonkeyengine:jme3-bullet:$jmeVersion"
    testRuntime "com.github.JavaSaBr.jmonkeyengine:jme3-bullet-native:$jmeVersion"
    testRuntime "com.github.JavaSaBr.jmonkeyengine:jme3-lwjgl3:$jmeVersion"

    testRuntime "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows"
    testRuntime "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux"
    testRuntime "org.lwjgl:lwjgl:${lwjglVersion}:natives-macos"
    testRuntime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows"
    testRuntime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux"
    testRuntime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-macos"
    testRuntime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:natives-windows"
    testRuntime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:natives-linux"
    testRuntime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:natives-macos"
    testRuntime "org.lwjgl:lwjgl-openal:${lwjglVersion}:natives-windows"
    testRuntime "org.lwjgl:lwjgl-openal:${lwjglVersion}:natives-linux"
    testRuntime "org.lwjgl:lwjgl-openal:${lwjglVersion}:natives-macos"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"

    testRuntime "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testRuntime "org.apache.logging.log4j:log4j-jul:$log4jVersion"

    // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
    testCompile "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

defaultTasks 'clean', 'install'

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.version = version
        pom.artifactId = 'jmonkeybuilder-extension'
        pom.project {
            licenses {
                license {
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}